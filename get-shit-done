#!/usr/bin/env zsh

function print_usage() {
    print -- "usage: get-shit-done STATE [TIME]\n" >&2
    print -- "Configure hosts file to either be in a work or play mode, for an optional timespan.\n" >&2
    print -- "Parameters:" >&2
    print -- "- STATE: work | play" >&2
    print -- "- TIME: MINUTES" >&2
    print -- "Example: remove the blacklisting for 30 minutes" >&2
    print -- "$ sudo get-shit-done play 30" >&2
}

(($#<1)) && {
    print_usage
    exit 1;
}

action="$1"
if [ "$2" ]; then
    timeout=`echo "60 * $2" | bc`
fi

blacklist=(
    'b.dk'
    'axios.com'
    'cnn.com'
    'facebook.com'
    'information.dk'
    'jp.dk'
    'linkedin.com'
    'netflix.com'
    'news.ycombinator.com'
    'nytimes.com'
    'politico.com'
    'politiken.dk'
    'reddit.com'
    'theguardian.com'
    'twitter.com'
    'washingtonpost.com'
    'youtube.com'
)

function add_blacklist() {
    for site in $blacklist; do
        hostess add www.$site 127.0.0.1
        hostess add $site 127.0.0.1
    done
}

function remove_blacklist() {
    for site in $blacklist; do
        hostess del www.$site
        hostess del $site
    done
}

function sleep_and_add_blacklist() {
    sleep $timeout
    echo "Adding blacklist again"
    add_blacklist
}

function sleep_and_remove_blacklist() {
    sleep $timeout
    echo "Removing blacklist again"
    remove_blacklist
}

function toggle_blacklist() {
case $action in
    h|help)
        print_usage
        ;;

    w|work)
        if [ -n "$timeout" ]; then
            echo "Adding blacklist for $timeout"
            add_blacklist
            sleep_and_remove_blacklist &
        else
            echo "Adding blacklist"
            add_blacklist
        fi
        ;;

    p|play)
        if [ -n "$timeout" ]; then
            echo "Removing blacklist for $timeout"
            remove_blacklist
            sleep_and_add_blacklist &
        else
            echo "Removing blacklist"
            remove_blacklist
        fi
        ;;

    *)
        {
            print -- "I don't know this option: '$action'"
            print_usage
            exit 1
        } >&2
esac
}

toggle_blacklist
